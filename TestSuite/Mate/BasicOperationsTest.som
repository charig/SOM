BasicOperationsTest = TestCase (
	"
		Message Sends/Activations are tested in ImmutabilityTest.
	 	Layout operations are tested in LayoutTest
	"
	
	setUp = (self class initialize)
	
	testLocalVarRead = (
		| localVar |
		localVar := 1.
		self assert: localVar equals: 1.
		self installLocalsMetaobject.
		self assert: localVar equals: 2.
		self uninstallMetaobject.
		self assert: localVar equals: 1.
    )
    
    testLocalVarWrite = (
		| localVar |
		localVar := 1.
		self assert: localVar equals: 1.
		self installLocalsMetaobject.
		localVar := 2.
		self uninstallMetaobject.
		self assert: localVar equals: 3.
		localVar := 2.
		self assert: localVar equals: 2.
    )
    
    testArgumentRead = (
		| argValue |
		argValue := self argsTest: 1.
		self assert: argValue equals: 1.
		self installArgumentsMetaobject.
		argValue := self argsTest: 1.
		self uninstallMetaobject.
		self assert: argValue equals: 2.
		argValue := self argsTest: 1.
		self assert: argValue equals: 1.
    )
    
    testReturn = (
    	| value |
    	self assert: self returnTest equals: 1.
    	self installEnvironment: self class returnMetaobject.
    	value := self returnTest.
    	self uninstallMetaobject.
    	self assert: value equals: 2.
		self assert: self returnTest equals: 1.
    )
    
    argsTest: aValue = (
    	^aValue
    )
    
    returnTest = (^1)
    
    installLocalsMetaobject = (
    	self installEnvironment: self class localMetaobject.
	)
	
	installArgumentsMetaobject = (
    	self installEnvironment: self class argumentsMetaobject.
	)
	
	uninstallMetaobject = (
    	self installEnvironment: nil.
	)
	
	------------------------------
	
	| localMetaobject argumentsMetaobject returnMetaobject |
	
	localMetaobject = (^localMetaobject)
	argumentsMetaobject = (^argumentsMetaobject)
	returnMetaobject = (^returnMetaobject)
	
	initialize = (
		localMetaobject := (EnvironmentMO 
				operationalSemantics: OperationalSemanticsForTestingLocals new 
				message: "MessageSemanticsForTest new" nil
				layout: nil).
				
		argumentsMetaobject	:= (EnvironmentMO 
				operationalSemantics: OperationalSemanticsForTestingArguments new 
				message: "MessageSemanticsForTest new" nil
				layout: nil).
		
		returnMetaobject := (EnvironmentMO 
				operationalSemantics: OperationalSemanticsForTestingReturn new 
				message: "MessageSemanticsForTest new" nil
				layout: nil)			
	)
 )
