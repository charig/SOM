CopyVisitor = FileSystemVisitor (
	"I create a copy of the directory tree that I visit. I use the PreorderGuide so that I can create directories before creating their contents. "
	| source dest |
	
	visitFile: anEntry = (
		| reference |
		reference := anEntry reference.
		reference = source
			ifTrue: [source copyTo: dest]
			ifFalse: [self copyFile: reference]
	)
	
	initializeWithSource: srcReference dest: dstReference = (
		self initialize.
		source := srcReference.
		dest := dstReference
	)
	
	visitDirectory: anEntry = (
		| reference |
		reference := anEntry reference.
		reference = source
			ifTrue: [dest ensureCreateDirectory]
			ifFalse: [self copyDirectory: reference]
	)
	
	copyDirectory: aReference = (
		| directory |
		directory := dest resolve: (aReference relativeTo: source).
		directory createDirectory
	)
	
	visit = (
		(PreorderGuide for: self) 
			show: source
	)
	
	copyFile: aReference = (
		| copy |
		copy := dest resolve: (aReference relativeTo: source).
		aReference copyTo: copy
	)
	
	----------------------------------
	
	copy: source to: dest = (
		(self from: source to: dest) visit
	)
	
	from: srcReference to: dstReference = (
		^ self basicNew
			initializeWithSource: srcReference 
			dest: dstReference
	)
)