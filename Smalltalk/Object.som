"

$Id: Object.som 27 2009-07-31 11:17:53Z michael.haupt $

Copyright (c) 2001-2013 see AUTHORS file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
"

Object = nil (
    class      = primitive
    objectSize = primitive  "size in bytes"
    copy = (^self shallowCopy postCopy)
    shallowCopy = primitive
    postCopy = (^ self) 

    "Comparing"

    " If you override =, you MUST override hashcode as well.  The rule
      obj1 = obj2   =>  obj1 hashcode = obj2 hashcode
      must be valid for all objects, or Hashtable will not work"
    =  other    = ( ^self == other )
    <> argument = ( ^(self = argument) not )
    == other    = primitive
    ~= other    = (^ (self == other) not )
    isNil       = ( ^false )
    notNil      = ( ^true )
    hash 		= (^self identityHash)
    identityHash = primitive
    
    isSymbol = ( ^false )
    isFloat  = ( ^true )
    
    "Accessing"
    species = (^self class)
    isKindOf: aClass = (
		self class == aClass
			ifTrue: [^true]
			ifFalse: [^self class inheritsFrom: aClass])
    
    "Converting"
    asString  = ( ^'instance of ' + (self class) )
    as: aSimilarClass = (^ aSimilarClass newFrom: self)
    , element = ( ^(Vector new append: self) append: element )
    hashcode  = primitive
    
    "Evaluating"
    value     = ( ^self )
    yourself  = ("Answer self." ^self)
    
    "Convenience"
    ifNil: aBlock = (^self)
    ifNotNil: aBlock = (^aBlock value: self)
    ifNil: noGoBlock ifNotNil: goBlock = (^goBlock value: self)
    
    "Printing"
    print     = ( self asString print )
    printString = (^String streamContents: [:s | self printOn: s])
    println   = ( self print. system printNewline )
	
	printOn: aStream = (
		| title |
		title := self class name.
		aStream nextPutAll: (title first isVowel ifTrue: ['an '] ifFalse: ['a ']).
		aStream	nextPutAll: title.
	)
		
    "Debugging"
    inspect   = primitive
    halt      = primitive
    
    "Error handling"
    error: string = ( '' println. ('ERROR: ' + string) println. system exit: 1 )
    
    "Abstract method support"
    subclassResponsibility = (
        self error: 'This method is abstract and should be overridden'
    )
    
    "Initialization"
    initialize = ()
    
    "Error recovering"
    doesNotUnderstand: selector arguments: arguments = (
        self error:
            ('Method ' + selector + ' not found in class ' + self class name)
    )
    
    escapedBlock: block = (
        self error: 'Block has escaped and cannot be executed'
    )
    
    unknownGlobal: name = ( ^system resolve: name )
    
    "Reflection"
    respondsTo: aSymbol = (
        (self class hasMethod: aSymbol)
            ifTrue:  [ ^true ]
            ifFalse: [ | cls |
                cls := self class superclass.
                [ cls isNil ] whileFalse: [
                    (cls hasMethod: aSymbol)
                        ifTrue:  [ ^true ]
                        ifFalse: [ cls := cls superclass ] ].
                ^ false ]
    )
    
    perform: aSymbol = primitive
    perform: aSymbol withArguments: args = primitive
    perform: aSymbol with: firstObject with: secondObject = ( 
		^ self perform: aSymbol withArguments: (Array with: firstObject with: secondObject)
	)
    perform: aSymbol inSuperclass: cls = primitive
    perform: aSymbol withArguments: args inSuperclass: cls = primitive
    
    instVarAt: idx          = primitive
    instVarAt: idx put: obj = primitive
    instVarNamed: sym       = primitive
    
    "Delegation Proxies"
    instVarNamed: sym myself: anObject = (^self instVarNamed: sym)
    instVarAt: anIndex myself: anObject = (^self instVarAt: anIndex)
    
    "Extension for the MOP"
	readOnly = ( ^Handle targetSPECIAL: self )
	changeShape: aShape = primitive
	shape = primitive
	
	"Debugging Stuff"
	printLastMethods: aNumber = (
		| ctx |
		ctx := thisContext.
		1 to: aNumber do: [:i | 
			ctx := ctx sender.
			ctx method println.
		]
	)
	
	"Petit Parser"
	isPetitFailure = (^false)
	isPetitParser = (^false)
	asParser = (^PPPredicateObjectParser expect: self)
	isString = (^false)
)
