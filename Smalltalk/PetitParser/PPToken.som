PPToken = Object (
	"PPToken represents a parsed part of the input stream. Contrary to a simple String it remembers where it came from, the original collection and its start and stop position.Instance Variables:	collection	<SequenceableCollection>	The collection this token comes from.	start	<Integer>	The start position in the collection.	stop	<Integer>	The stop position in the collection."
	| collection start stop value |
	
	copyFrom: aStartInteger to: aStopInteger = (
		^ self class on: collection start: start + aStartInteger - 1 stop: stop + aStopInteger - 3 value: value
	)
	
	column = (
		"Answer the column number of this token in the underlying collection."
		
		| position |
		position := 0.
		(PPToken NewLineParser , [ :stream |
			start <= stream position
				ifTrue: [ ^ start - position ].
			position := stream position ] asParser
			/ #any asParser) star
				parse: collection.
		 ^ start - position
	)
	
	hash = (^ self inputValue hash)
	parsedValue = (^ value)
	size = (^ stop - start + 1)
	stop = (^ stop)
	start = (^ start)
	collection = (^ collection)
	inputValue = (^ collection copyFrom: start to: stop)
	
	line = (
		"Answer the line number of this token in the underlying collection."
		
		| line |
		line := 1.
		(PPToken NewLineParser , [ :stream |
			start <= stream position
				ifTrue: [ ^ line ].
			line := line + 1 ] asParser
			/ #any asParser) star
				parse: collection.
		^ line
	)
	
	= anObject = (^ self class = anObject class and: [ self inputValue = anObject inputValue ])
	
	printOn: aStream = (
		super printOn: aStream.
		aStream nextPut: '['.
		aStream	print: self start.
		aStream	nextPut: ','.
		aStream	print: self stop.
		aStream	nextPut: ']'.
		aStream nextPut: '('.
		aStream print: self inputValue.
		aStream	nextPut: ')'.
	)
	
	initializeOn: aSequenceableCollection start: aStartInteger stop: aStopInteger value: anObject = (
		collection := aSequenceableCollection.
		start := aStartInteger.
		stop := aStopInteger.
		value := anObject
	)
	
	----------------------------------
	| NewLineParser |
	
	initialize = (
		"Platform independent newline sequence. LF: Unix, CR+LF: Windows, and CR: Apple."
		| cr lf |
		cr := Character codePoint: 13.
		lf := Character codePoint: 10.
		NewLineParser := lf asParser / (cr asParser , lf asParser optional)
	)
	
	NewLineParser = (^NewLineParser)
	
	new = (
		self error: 'Token can only be created using a dedicated constructor.'
	)
	
	on: aSequenceableCollection start: aStartInteger stop: aStopInteger value: anObject = (
		^ self basicNew 
			initializeOn: aSequenceableCollection
			start: aStartInteger stop: aStopInteger
			value: anObject
	)
	
	on: aSequenceableCollection = (
		^ self on: aSequenceableCollection start: 1 stop: aSequenceableCollection length value: nil
	)
)