RBToken = Object (
	"RBToken is the abstract superclass of all of the RB tokens. These tokens (unlike the standard parser's) remember where they came from in the original source code.

Subclasses must implement the following messages:
	accessing
		length

Instance Variables:
	sourcePointer	<Integer>	The position in the original source code where this token began.
"
	| sourcePointer comments |
	
	isIdentifier = (^false)
	isKeyword = (^false)
	isPatternVariable = (^false)
	isLiteralArrayToken = (^false)
	isBinary = (^false)
	isSpecial = (^false)
	isAssignment = (^false)
	isLiteralToken = (^false)
	isPatternBlock = (^false)
	isError = (^false)
	
	removePositions = (sourcePointer := nil)
	start = (^ sourcePointer ifNil: [ 0 ])
	start: anInteger = (sourcePointer := anInteger)
	length = (^self subclassResponsibility)
	comments = (^comments)
	comments: anObject = (comments := anObject)
	
	stop = (
		^ sourcePointer 
			ifNil: [ -1 ]
			ifNotNil: [ self start + self length - 1 ]
	)
	
	printOn: aStream = (
		aStream nextPut: $ .
		aStream	nextPutAll: self class name
	)
	
	----------------------------------
	
	start: anInterval = (^self new start: anInterval)
)
