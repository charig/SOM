RBLiteralValueNode = RBLiteralNode (
	"RBLiteralNode is an AST node that represents literal values (e.g., #foo, true, 1, etc.), but not literal arrays.

Instance Variables
	value	<Numeric | Symbol | String  | Character>	the literal value I represent"
	| value sourceText |
	
	value = (^ value)
	isFaulty = (^false.)
	acceptVisitor: aProgramNodeVisitor = (^aProgramNodeVisitor visitLiteralNode: self)
	copyInContext: aDictionary = (^ self class value: self value)
	specialCommands = (^ SugsSuggestionFactory commandsFoLiteralNode)
	
	= anObject = (  
		^ super = anObject 
			and: [ self value = anObject value 
			and: [ self value species = anObject value species ] ]
	)
	
	value: aValue start: aPosition stop: anotherPosition source: aString = (
		self value: aValue start: aPosition stop: anotherPosition.
		sourceText := aString.
	)
	
	sourceText = (
		^ sourceText ifNil: [
			String streamContents: [:aStream|			
				value isSymbol
					ifTrue: [
						aStream nextPut: $#.
						((RBScanner isSelector: value) and: [value ~~ #'||']) 
							ifTrue: [aStream nextPutAll: value]
							ifFalse: [value asString printOn: aStream]]
					ifFalse: [ 
						value class == Character 
							ifTrue: 
								[aStream nextPut: $$. stream nextPut: value]
							ifFalse: [ value storeOn: aStream ]]] ]
	)
	
	value: aValue start: aPosition stop: anotherPosition = (
		value := aValue.
		start := aPosition. 
		stop := anotherPosition.
	)
	
	----------------------------------
	
	value: aValue start: aPosition stop: anotherPosition = (
	 	| node |
	 	node := self new.
	 	node value: aValue start: aPosition stop: anotherPosition.  
	 	^node
	)
	
	value: aValue = (
		^ self value: aValue start: 0 stop: -1
	)
	
	value: aValue start: aPosition stop: anotherPosition  source: aString = (
	 	| node |
	 	node := self new.
	 	node value: aValue start: aPosition stop: anotherPosition source: aString.
	 	^node.
	)
)
